import groovy.sql.Sql
import org.h2.Driver
import org.jooq.codegen.GenerationTool
import org.jooq.meta.jaxb.*

buildscript {
    ext.kotlin_version = '1.7.21'
    ext.jooq_version = '3.17.5'
    ext.h2_version = '1.4.187'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jooq:jooq-codegen:$jooq_version"
        classpath "com.h2database:h2:$h2_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.7.21"
    id "org.jetbrains.kotlin.plugin.serialization" version '1.7.21'
    id 'java-library'
    id 'maven-publish'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

group = 'com.github.sungmin-park'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jooq:jooq:$jooq_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"

    testImplementation "com.h2database:h2:$h2_version"
    testImplementation 'junit:junit:4.11'
}

def sql = new Sql(new Driver().connect('jdbc:h2:mem:test-jooq-tools', null))
try {
    sql.execute(file('src/test/resources/db.sql').text)
    //noinspection GrDeprecatedAPIUsage
    def configuration = new Configuration()
    configuration
            .withJdbc(new Jdbc().withUrl('jdbc:h2:mem:test-jooq-tools'))
            .withGenerator(
                    new Generator()
                            .withDatabase(new Database().withInputSchema('PUBLIC'))
                            .withGenerate(new Generate().withImmutablePojos(true))
                            .withTarget(new Target()
                                    .withPackageName('com.github.parksungmin.jooqs.database')
                                    .withDirectory('src/test/java')
                            )
            )
    GenerationTool.generate(configuration)
} finally {
    sql.close()
}
